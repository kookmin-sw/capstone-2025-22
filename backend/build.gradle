plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.capstone'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}
// root 모듈에 대한 spring boot jar 생성 방지
bootJar {
	enabled = false
}
// root 모듈에 대한 plain jar 생성 방지
jar {
	enabled = false
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}
// ext
ext {
	set('springCloudVersion', "2023.0.2")
}
subprojects {
	// group
	group = 'com.capstone'
	// plugin
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	// repository
	repositories {
		mavenCentral()
	}
	// java version
	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}
	// plain jar 생성 방지
	jar {
		enabled=false
	}
	// 모듈 내부에 spring boot jar 생성
	bootJar{
		enabled=true
	}
	// dependency management
	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}
	// public dependencies
	dependencies {
		// lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		// logging
		implementation 'org.slf4j:slf4j-api'
		// swagger-webflux
		implementation("org.springdoc:springdoc-openapi-starter-webflux-ui:2.0.4")
		// actuator
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
	}

	tasks.withType(JavaCompile).tap {
		configureEach {
			options.encoding = 'UTF-8'
		}
	}
}